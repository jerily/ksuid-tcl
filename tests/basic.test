package require tcltest
package require ksuid

namespace import -force ::tcltest::test

::tcltest::configure {*}$argv

test basic-1 {length of ksuid} -body {
    set ksuid [::ksuid::generate_ksuid]
    string length $ksuid
} -result {27}

test basic-2 {generate ksuid, get its parts, create ksuid again from parts and compare} -body {
    set ksuid [::ksuid::generate_ksuid]
    set parts [::ksuid::ksuid_to_parts $ksuid]
    set ksuid_from_parts [::ksuid::parts_to_ksuid $parts]
    expr {$ksuid eq $ksuid_from_parts}
} -result {1}

test basic-3 {next of a prev of a ksuid and prev of a next of a ksuid are equal} -body {
    set ksuid [::ksuid::generate_ksuid]
    set prev_of_next_ksuid [::ksuid::prev_ksuid [::ksuid::next_ksuid $ksuid]]
    set next_of_prev_ksuid [::ksuid::next_ksuid [::ksuid::prev_ksuid $ksuid]]
    expr {$ksuid eq $prev_of_next_ksuid && $ksuid eq $next_of_prev_ksuid}
} -result {1}

test basic-4 {next of max is zero} -body {
    set ksuid "aWgEPTl1tmebfsQzFP4bxwgy80V"
    ::ksuid::next_ksuid $ksuid
} -result {000000000000000000000000000}

test basic-5 {prev of zero is max} -body {
    set ksuid "000000000000000000000000000"
    ::ksuid::prev_ksuid $ksuid
} -result {aWgEPTl1tmebfsQzFP4bxwgy80V}

test error-1 {invalid ksuid} -body {
    set ksuid "aaaaaaaaaaaaaaaaaaaaaaaaaaa"
    ::ksuid::ksuid_to_parts $ksuid
} -returnCodes error -result {invalid base62}

test error-2 {invalid base62} -body {
    set ksuid "aWgEPTl1tmebfsQzFP4bxwgy80!"
    ::ksuid::ksuid_to_parts $ksuid
} -returnCodes error -result {invalid base62}

test error-3 {invalid payload length} -body {
    set d [dict create timestamp 0 payload 0]
    ::ksuid::parts_to_ksuid $d
} -returnCodes error -result {invalid hex}

test error-4 {invalid hex} -body {
    set d [dict create timestamp 0 payload 00]
    ::ksuid::parts_to_ksuid $d
} -returnCodes error -result {invalid hex}

test error-5 {invalid timestamp} -body {
    set d [dict create timestamp abc payload ""]
    ::ksuid::parts_to_ksuid $d
} -returnCodes error -result {expected integer but got "abc"}

test error-5 {invalid hex} -body {
    set d [dict create timestamp 0 payload ""]
    ::ksuid::parts_to_ksuid $d
} -returnCodes error -result {invalid hex}

test correct-parts-1 {check correct parts} -body {
    set d [dict create timestamp 294295716 payload "5b4bd92eeb34c91060ebd36a32738f03"]
    ::ksuid::parts_to_ksuid $d
} -result {2VB3bNOnJhCPqYfm9UQwV90tyTb}

test correct-parts-2 {check correct parts} -body {
    set d [dict create timestamp 294295845 payload "2e6ab5cfe184cf97c5db875c5f6b3570"]
    ::ksuid::parts_to_ksuid $d
} -result {2VB3rZ7VN8syq2WsQDdWt6DdvSi}

proc sleep {millis} {
    after $millis set stop_wait &
    vwait stop_wait
}

test compare-ksuids-1 {compare ksuids} -body {
    set ksuid1 [::ksuid::generate_ksuid]

    sleep 2000

    set ksuid2 [::ksuid::generate_ksuid]
    expr {$ksuid1 ne $ksuid2 && $ksuid1 < $ksuid2}
} -result {1}


test compare-ksuids-2 {compare ksuid timestamps} -body {
    set ksuid1 [::ksuid::generate_ksuid]

    sleep 2000

    set ksuid2 [::ksuid::generate_ksuid]

    set parts1 [::ksuid::ksuid_to_parts $ksuid1]
    set parts2 [::ksuid::ksuid_to_parts $ksuid2]

    set timestamp1 [dict get $parts1 timestamp]
    set timestamp2 [dict get $parts2 timestamp]

    expr {$timestamp1 < $timestamp2}
} -result {1}